if (isHttpRunAlone.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
android {

    compileSdkVersion versions.compileSdkVersion
    buildToolsVersion "${versions.buildToolsVersion}"

    defaultConfig {
        if (isHttpRunAlone.toBoolean()) {
            applicationId "com.divine.yang.httpcomponent"
        }
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        flavorDimensions "versionCode"
        productFlavors {
            DEFAULT {
                versionCode moudleHttpVersions.versionCode
                versionName moudleHttpVersions.versionName
                manifestPlaceholders = [
                        app_name: "Retrofit2应用",
                        logo    : "@mipmap/ic_launcher",
                        icon    : "@mipmap/ic_launcher"
                ]
            }
        }
    }
    sourceSets {
        main {
            if (isHttpRunAlone.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }else {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    if (isHttpRunAlone.toBoolean()) {
        android.applicationVariants.all {
            variant ->
                variant.outputs.all {
                    def buildName
                    def versionName
                    def releaseTime = new Date().format("yyMMddHHmm")
                    variant.productFlavors.each { product ->
                        buildName = product.name
                        versionName = product.versionName
                    }
                    def fileName = "HTTP_${buildName}_V${versionName}.${releaseTime}.apk"
                    outputFileName = fileName
                }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(":basecomponent")

    if (isHttpRunAlone.toBoolean()) {
        implementation retrofit2.Retrofit
        implementation retrofit2.ConverterGson
        implementation retrofit2.ConverterScalars
        implementation retrofit2.AdapterRxjava2
        implementation rxjava2.Rxjava
        implementation rxjava2.Rxandroid
    }else{
        api retrofit2.Retrofit
        api retrofit2.ConverterGson
        api retrofit2.ConverterScalars
        api retrofit2.AdapterRxjava2
        api rxjava2.Rxjava
        api rxjava2.Rxandroid
    }

    annotationProcessor wmRouter.RouterCompiler
    annotationProcessor glide.GlideCompiler
}
