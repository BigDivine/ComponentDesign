if (isLoginRunAlone.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {

    compileSdkVersion versions.compileSdkVersion
    buildToolsVersion "${versions.buildToolsVersion}"

    defaultConfig {
        if (isLoginRunAlone.toBoolean()) {
            applicationId "com.divine.yang.nmjc_login"
        }
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        flavorDimensions "versionCode"
        productFlavors {
            DEFAULT {
                versionCode moudleLoginVersions.versionCode
                versionName moudleLoginVersions.versionName
                manifestPlaceholders = [
                        app_name: "Login应用",
                        logo    : "@mipmap/ic_launcher",
                        icon    : "@mipmap/ic_launcher"
                ]
            }
        }
    }

    sourceSets {
        main {
            if (isLoginRunAlone.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }else {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    if (isLoginRunAlone.toBoolean()) {
        android.applicationVariants.all {
            variant ->
                variant.outputs.all {
                    def buildName
                    def versionName
                    def releaseTime = new Date().format("yyMMddHHmm")
                    variant.productFlavors.each { product ->
                        buildName = product.name
                        versionName = product.versionName
                    }
                    def fileName = "LOGIN_${buildName}_V${versionName}.${releaseTime}.apk"
                    outputFileName = fileName
                }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(":lib_base")
    implementation project(":lib_widget")

    implementation project(":nmjc_router")

    annotationProcessor wmRouter.RouterCompiler
    annotationProcessor glide.GlideCompiler
}
