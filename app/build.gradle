apply plugin: 'com.android.application'
// 应用WMRouter插件
apply plugin: 'WMRouter'
android {

    compileSdkVersion versions.compileSdkVersion
    buildToolsVersion "${versions.buildToolsVersion}"

    defaultConfig {
        applicationId "com.divine.yang.componentdesign"

        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        flavorDimensions "versionCode"
        productFlavors {
            DEFAULT {
                versionCode appVersions.versionCode
                versionName appVersions.versionName
                manifestPlaceholders = [
                        app_name: "云报账",
                        logo    : "@mipmap/ic_launcher",
                        icon    : "@mipmap/ic_launcher"
                ]
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                def buildName
                def versionName
                def releaseTime = new Date().format("yyMMddHHmm")
                variant.productFlavors.each { product ->
                    buildName = product.name
                    versionName = product.versionName
                }
                def fileName = "CLOUD_${buildName}_V${versionName}.${releaseTime}.apk"
                outputFileName = fileName
            }
    }
}

dependencies {

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(":lib_base")

    implementation project(":module_splash")
    //    #camera2相机拍照模块是否独立运行
    if (!isCamera2RunAlone.toBoolean()) {
        implementation project(":module_camera2")
    }
    //    #网络请求模块是否独立运行
    if (!isHttpRunAlone.toBoolean()) {
        implementation project(":lib_http")
    }
    //    #仿微信图片编辑模块是否独立运行
    if (!isImageEditRunAlone.toBoolean()) {
        implementation project(":module_imageedit")
    }
    //    #登录模块是否独立运行
    if (!isLoginRunAlone.toBoolean()) {
        implementation project(":nmjc_login")
    }
    //    #数据库管理模块是否独立运行
    if (!isSqliteRunAlone.toBoolean()) {
        implementation project(":lib_sqlite")
    }
    //    #自定义控件模块是否独立运行
    if (!isWidgetRunAlone.toBoolean()) {
        implementation project(":lib_widget")
    }
    //    #自定义控件模块是否独立运行
    if (!isWebViewRunAlone.toBoolean()) {
        implementation project(":module_webview")
    }
    annotationProcessor wmRouter.RouterCompiler
    annotationProcessor glide.GlideCompiler
}